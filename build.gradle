plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.spotbugs" version "4.7.1"
}

group = 'gr.james'
version = '0.23'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

spotbugs {
    spotbugsTest.enabled = false
    effort = "max"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Random Sampling'
                description = 'A collection of algorithms in Java 8 for the problem of random sampling with a reservoir'
                url = 'https://github.com/gstamatelat/random-sampling'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'gstamatelat'
                        name = 'Giorgos Stamatelatos'
                        email = 'gstamatelat@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/gstamatelat/random-sampling.git'
                    developerConnection = 'scm:git:git@github.com:gstamatelat/random-sampling.git'
                    url = 'https://github.com/gstamatelat/random-sampling'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("${buildDir}/publishing-repository")
        }
    }
}

wrapper {
    gradleVersion = '6.5.1'
}

javadoc {
    doLast {
        new File(destinationDir, 'stylesheet.css').append(file('src/javadoc/stylesheet.css').text)
        new File(destinationDir, 'script.js').append(file('src/javadoc/script.js').text)
    }
    options.overview('src/javadoc/overview.html')
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}
